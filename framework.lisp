(in-package :mu-cl-resources)

;;;;;;;;;;;;;;;;;;
;;;; configuration
(defparameter *camelcase-json-variables* nil
  "when non-nil, json variable names should be camelcased, rather than dasherized.")
(defparameter *verify-accept-header* nil
  "when non-nil, the application/vndi+json ACCEPT header is checked.")

;;;;;;;;;;;;;;;;
;;;; error codes

(define-condition no-such-resource (error)
  ((description :initarg :description :reader description))
  (:documentation "Indicates the resource could not be found"))

(define-condition no-such-instance (error)
  ((type :initarg :type :reader target-type)
   (id :initarg :id :reader target-id)
   (resource :initarg :resource :reader resource))
  (:documentation "Indicates the resource could not be found"))

(define-condition no-such-link (error)
  ((resource :initarg :resource :reader resource)
   (path :initarg :path :reader path))
  (:documentation "Indicates the specified link does not exist
    for the supplied resource."))

(define-condition simple-described-condition (error)
  ((description :initarg :description :reader description))
  (:documentation "Indicates an exception which should mainly be
    handled by its type and a base description."))

(define-condition incorrect-accept-header (simple-described-condition)
  ()
  (:documentation "Indicates a necessary accept header was not found."))

(define-condition incorrect-content-type (simple-described-condition)
  ()
  (:documentation "Indicates a necessary content-type header was not found."))

(define-condition invalid-link-patch-body-format (simple-described-condition)
  ()
  (:documentation "Indicates the patch body for a link update was not correct."))

(define-condition no-type-in-data (error)
  ()
  (:documentation "Indicates no type property was found in the primary data"))

(define-condition id-in-data (error)
  ()
  (:documentation "Indicates an id property was found in the
    primary data whilst it was not expected."))

(define-condition no-id-in-data (error)
  ()
  (:documentation "Indicates no id property was found in the
    primary data whilst it was expected."))

(define-condition request-type-mismatch (error)
  ((path-defined-type :initarg :path-defined-type :reader path-defined-type)
   (content-defined-type :initarg :content-defined-type :reader content-defined-type))
  (:documentation "Indicates the type in the request does not match the type
    of the supplied content."))

(define-condition request-id-mismatch (error)
  ((path-defined-id :initarg :path-defined-id :reader path-defined-id)
   (content-defined-id :initarg :content-defined-id :reader content-defined-id))
  (:documentation "Indicates the id in the request does not match
    the id of the supplied content."))


;;;;;;;;;;;;;;;;;;;;
;;;; Supporting code

(defun symbol-to-camelcase (content &key (cap-first nil))
  "builds a javascript variable from anything string-like"
  (format nil "窿戾è汜瓠铄汜瓠骈蝮舂祜镳骘汨狎徙蝻篌篝蜷铉滹黝汜箦篝蜷铉泔铘孱舂殒ㄣ栳蚪汨狎＼滹箦翩汜瓠铄舂屐箦泔祆邈痱镧ㄩ汜瓠铄ㄣ栳颦躔汜箦汨狎汨狎箦翩汜瓠铄铋飑┅┅ㄤ彐躅礤蜱瀛牦秣瞽镡赍泗ㄡ猢⑼弪珏牦秣镡赍泗犷麸珏翳弪义趱蝾铄镡赍泗麒殂泔铘衢铙翳礤蜱邃泔铘孱趔戾è脲躅轱牦秣詈脲黠蜾岍牦秣詈脲黠蜾猢呼弩＇篝蜷铉僵蝈篚祠牦秣詈屙痿镡赍泗┅祜镳骘脲轭脲滹ㄣ镱è犷牦秣詈脲脲铒牦秣詈脲脲┅箦翩牦秣詈鲠蝈篚祠脲牦秣詈鲠脲┅è犷铒牦秣詈脲脲┅牦秣詈脲脲┅箦翩牦秣詈鲠蝈篚祠脲牦秣詈鲠脲┅ㄨ犷潇弪汜箦箦翩牦秣詈鲠蝈篚祠脲礤蜱瀛牦秣瞽镡赍泗牦秣詈鲠脲牦秣詈鲠脲┅ㄥ蝌矧ī箦翩牦秣詈鲠蝈篚祠脲牦秣詈鲠脲┅┅┅蝈篚祠┅换换换换换换换换换换换换换换换换聃弪屮邈豸轱桢祓弪ㄤ彐疳蜥礤翦聃弪珧秕皙铋⒛弩泸殁弩珧秕镦聃弪殄麸忮屮邈豸邃轭镱顼澡聃弪殄狎屮邈豸邃麒孱翳聃弪珧秕轶孱溴洚澡轶轶箴邈獒鲠蜷徕戾麒殂轶箦怡鏖翳珧秕疱洵聃弪殄螈ㄤ彐磲泸鏖翳聃弪珧秕é怙澌怙澌⒂翎螋铄聃弪珧秕甬澡聃弪殄狎屮邈豸邃麒孱翳珧秕屮轸螽啜戾è聃弪珧秕皙ㄣ镱铋铋飑┅棱镤戾è聃弪殄ㄡ痧禊＇螳蝈鲥蝮ㄢ豸灬篝聃弪珧秕皙┅┅ㄦ躞咫楹鏖翳聃弪祜珑轭弪蝻颦秕麴豸ㄦ躞咫楹聃弪蝈痫箝麸蝙聃弪殄螬┅┅ㄤ彐躅箴狎耢聃弪ㄣ镱翦铘⑴邈豸弩箴狎耢聃弪镱翳沲蝌孱蝈痫箝麸蝙矧瘐箬弩轸镱翳箦镦聃弪殄麸忮屮邈豸邃轭翳聃弪珧秕忉箦镱翳沲蝌孱泔铘屮舢蜗耘箦箴狎耢箦戾泗箴狎耢轭箦螋犷箴狎耢溴戾翦骘骢钽糸镱珏狎邃麸麽蜾孱洵躞弪螽ㄩ聃弪珧秕皙瘐箬泔铘孱聃弪珧秕皙ㄦ躞咫楹鏖翳聃弪祜珑轭弪蝻颦秕麴豸ㄦ躞咫楹聃弪蝈痫箝麸蝙泔铘孱舂┅ㄤ彐躅箴狎耢箦戾泗鲠蜷徕戾怙澌⑴邈豸弩有烈烟优膛迷聃弪镱翳沲蝌孱珧狃璁葬脲鏖翳聃弪珧秕轭麸徙泔躅舢箴狎耢聃弪蟓箦戾泗鲠蜷徕戾蟓珧狃蟓躜㈣趑鸷眭箦眙瀹汨狃痨殂狒轱畀怙澌┅┅ㄤ彐躅箴狎耢轭箦螋ㄢ镤⑴邈豸弩有烈烟晌优以牧粤聃弪镱翳沲蝌孱珧狃璁葬脲鏖翳聃弪珧秕轭麸徙泔躅舢箴狎耢聃弪蟓轭箦螋蟓珧狃蟓躜㈣趑鸷眭箦眙瀹汨狃痨殂狒轱畀怙澌┅┅ㄤ彐躅箴狎耢溴戾翦ㄢ镤⑴邈豸弩有烈烟呐膛耘聃弪镱翳沲蝌孱珧狃璁葬脲鏖翳聃弪珧秕轭麸徙泔躅舢箴狎耢聃弪蟓溴戾翦蟓珧狃蟓躜㈣趑鸷眭箦眙瀹汨狃痨殂狒轱畀怙澌┅┅换换换换换换换换换换换换换换换疳蝮轭聃弪蝈篚祠ㄤ彐躅骝镯箴狎耢镡赍泗⒚镱鲥螋翳篚痧扉邃箴狎耢鲠祯箴邈殒殂狒轱轭麸扉箴鲠祯瀹戾è豉疱ㄩ铘弪篝蜷铉躔汜箦牦秣詈鲠镡赍泗Ⅳ疱┅弘妁黠蜾┅鲠祯牦秣詈鲠镡赍泗Ⅵ犰蹂┅ㄩ眇矧舡鲠祯瀛骝镯箴狎耢蝈篚祠豉疱鲠祯镡赍泗┅ㄤ彐珏铄蜷轫痫螋鲠祯瀛骝镯箴狎耢蝈篚祠豉疱鲠祯镡赍泗ê滹沲礤铘狒轱㈤眇矧趔翳鲠祯骝镯э怅邈臾玳鲥轸鲠祯濮犷豉疱麸溟箴狒汨镱ê礤翳镤è豉疱ㄥ耢乎蜷┅鲠祯镡赍泗ㄤ邈灬蝈ㄩ珙矧镡赍泗┅鲠祯濠ê礤翳镤è豉疱ㄥ耢红轸弪犰┅鲠祯镡赍泗ㄤ邈灬蝈ㄩ珙矧镡赍泗┅鲠祯濠ê礤翳镤è豉疱ㄥ耢呼疱洵扉翦蜥飑鲠祯镡赍泗ㄩ眇矧舡豉疱洵扉翦蜥飙鲠祯瀛骝镯箴狎耢蝈篚祠牦秣詈鲠镡赍泗溽翎豉疱鲠祯镡赍泗┅ㄤ彐疳蜥礤翦豉疱洵扉翦蜥飙轫痫螋弪螵磲脲栳箬翎忪呼弩у聃犰后钽栩镱辁邃舂泔铘衢铙犰泔铞弪麸蝮骘豉疱洵扉翦蜥鲠祯弩泔黹铉骝镯翳溽翎忉箦ㄤ彐磲泸溴骈铄豉疱洵扉翦蜥飙轫痫螋弪豉疱é蝈篝鲠蜷徕戾螬怙澌怙澌溴骈铄铄豉疱扉翦蜥轫痫螋弪箬秕熹蝈沐轹鲠祯瀣镡赍泗狍鲠蜷徕戾螽啜箦翩ㄧ弭栳箬豉疱豉疱洵扉翦蜥飙轫痫螋弪螵灬礅溽ì丽狎獒忪弩棱镤┅ㄤ彐躅轫痫螋豉疱洵扉翦蜥飙鲠祯瀛骝镯箴狎耢蝈篚祠豉疱鲠祯镡赍泗㈤眇矧趔豉疱洵扉翦蜥飙鲠祯骝镯箴狎耢蝈篚祠ㄦ躅汜祆ㄧ弭栳箬豉疱豉疱洵扉翦蜥飙轫痫螋弪螵鲠祯镡赍泗┅ㄤ彐轭瀛豉疱洵扉翦蜥飙轫痫螋弪㈣趑鸷鼢鳟鞒矧绡舶氨赝逃汨屙幔溴汩磲膦鲠祯镡赍泗ㄤ邈灬蝈ㄩ珙矧镡赍泗┅蝈徜骝镯篝蜷铉鲠祯濠ㄤ彐轭瀛豉疱洵扉翦蜥飙轫痫螋弪㈣趑鸷鼢鳟鞒矧绡舶氨赝逃汨屙幔轭翦珏颌鲠祯镡赍泗ㄤ邈灬蝈ㄩ珙矧镡赍泗┅疳蝮瀛轭翦珏鲠祯濠ㄤ彐轭瀛豉疱洵扉翦蜥飙轫痫螋弪㈣趑鸷鼢鳟鞒矧绡舶氨赝逃汨屙幔怙镬遽睥鲠祯镡赍泗ㄤ邈灬蝈ㄩ珙矧镡赍泗┅ㄩ矧篝蜷铉鲠祯⒈篝蜷铉鲠祯Ⅳ蝓澧┅呼蝓烘犰箦┅ㄤ彐轭瀛豉疱洵扉翦蜥飙轫痫螋弪㈣趑鸷眭箦眙瀹汨鲲汜怩灬蜷弩豉疱洵扉翦蜥祗怙镬遽睥鲠祯镡赍泗ㄤ邈灬蝈ㄩ珙矧镡赍泗┅ㄩ矧篝蜷铉鲠祯⒈篝蜷铉鲠祯Ⅳ蝓澧┅呼蝓烘犰箦┅换换换换换换换换换换换换换溴骈铋铉蝈箫躜沐ㄤ彐沆狍蝈箫躜沐箪雉īè牦镱脲洪铋翎蜱宏箫瞽脲候遽溴牦镱脲熹痱镳弪豉洪铋翎蜱红洵痱镳弪豉候遽溴熹痱镳弪豉蝈箫躜沐豉疱洪铋翎蜱候弩秕蜚瀛豉疱候遽溴蝈箫躜沐豉疱┅ê滹沲礤铘狒轱⒛弩泸殁弩箝铉戾痱镳弪豉镦蝈箫躜沐┅ㄤ彐沆狍栳蟓扉铍īè蝈箫躜沐钺礤洪铋翎蜱候弩秕蜚候遽溴蝈箫躜沐钺礤熹扉铍洪铋翎蜱忽獒候遽溴熹扉铍ㄩ铞弪箦洪铋翎蜱洪铞弪箦候遽溴轭鲥蝮瀛洪铋翩矧铋飑蝈聃弩舡疳翳洪铋翎蜱横候遽溴蝈聃弩舡疳翳┅ê滹沲礤铘狒轱⒛弩泸殁弩扉铍麸犷雉桢蝈箫躜沐亠箬秕熹躞镱镦轸篚忏灬篌弩┅ㄤ彐沆狍栳蟓磲铢扉铍ㄨ狍扉铍īê滹沲礤铘狒轱⒛弩泸殁弩栳蟓磲铢扉铍麸犷雉桢蝈箫躜沐┅ㄤ彐沆狍栳蟓镱瀛扉铍ㄨ狍扉铍īê滹沲礤铘狒轱⒛弩泸殁弩栳蟓镱扉铍麸犷雉桢蝈箫躜沐┅ㄤ彐沆狍蝈箫躜沐īè熹沆狍洪铋翎蜱红洵沆狍候遽溴熹沆狍螬熹痱镳弪糸弩洪铋翎蜱红洵痱镳弪糸弩候遽溴熹痱镳弪糸弩熹蝈箫躜沐忉箦洪铋翎蜱红洵蝈箫躜沐忉箦候遽溴熹蝈箫躜沐忉箦牦镱豉疱洪铋翎蜱宏箫瞽豉疱候遽溴牦镱豉疱ㄨ狍磲铢扉铍洪铋翎蜱鸿狍磲铢候遽溴栳蟓磲铢扉铍螬ㄨ狍镱瀛扉铍洪铋翎蜱鸿狍镱候遽溴栳蟓镱瀛扉铍螬蝈聃弩舡疳翳洪铋翎蜱候羼蹂篝疳翳候遽溴蝈聃弩舡疳翳┅ㄤ彐珏铄蜷牦镱痱镳弪豉钺礤蝈箫躜沐箪雉ê滹沲礤铘狒轱Ⅱ弭蜷弼弩翳钺礤镦翳牦镱痱镳弪豉镦翳篚痧扉邃蝈箫躜沐箪雉ê礤翳镤è箪雉蝈箫躜沐箪雉┅ㄩ汜礤煦狍瀛牦镱鲠蜷徕戾螵簌礅镬麸汜礤煦狍牦镱脲箪雉┅篝蜷铉滹黝汜箦篝蜷铉牦镱脲箪雉┅┅┅ㄤ彐珏铄蜷箴狎耢鲠蜷徕戾钺礤蝈箫躜沐箪雉ê滹沲礤铘狒轱Ⅱ弭蜷弼弩翳钺礤镦翳牦镱痱镳弪豉狍轸泔蹯忮躞邃轭箴狎耢聃弪ê礤翳镤è箪雉蝈箫躜沐箪雉┅簌礅镬麸汜礤煦狍牦镱脲箪雉┅┅ㄤ彐珏铄蜷熹痱镳弪豉扉篝箪雉ê滹沲礤铘狒轱Ⅸ殄熹翳熹痱镳弪豉狍扉篝骝镯翳蝈箫躜沐箪雉ê礤翳镤è箪雉蝈箫躜沐箪雉┅扉篝熹痱镳弪豉箪雉┅┅ㄤ彐礤翳镤牦镱脲è扉铍栳蟓扉铍┅蝈聃弩舡疳翳扉铍┅ㄤ彐珏铄蜷骈钿扉铍怡牦镱钺礤蝈箫躜沐牦镱扉铍ê滹沲礤铘狒轱㈡轭栳蟓磲铢扉铍怡蝈箫躜沐犷牦镱扉铍镦翳扉铍ê礤翳镤è蝈箫躜沐蝈箫躜沐牦镱扉铍祜镳骘扉铍轭ㄡ祆扉铍蝈箫躜沐殒篝蜷铉牦镱脲扉铍牦镱扉铍蝈趱蝾扉铍┅ㄤ彐珏铄蜷犰飙扉铍蝈箫躜沐ê滹沲礤铘狒轱⒁弭蜷弼弩犰扉铍骘翳篚痧扉邃蝈箫躜沐嘛翳翳栳蟓磲铢扉铍犷栳蟓镱瀛扉铍螽ê礤翳镤è蝈箫躜沐蝈箫躜沐┅ㄡ痧孱ㄨ狍磲铢扉铍蝈箫躜沐ㄨ狍镱瀛扉铍蝈箫躜沐┅┅ㄤ彐珏铄蜷蝈箫躜沐箪雉怡牦镱脲蝈箫躜沐脲ê滹沲礤铘狒轱⒁弭躜铙翳箪雉麒殂箬秕熹忮泔眄躅殂狒邃鏖翳翳牦镱骘蝽狒翳蝻蹒翳躞镦翳脲狒趄殁豸瀹ê礤翳镤è蝈箫躜沐蝈箫躜沐脲祜镳骘箪雉轭熹痱镳弪糸弩蝈箫躜沐麒孱篝蜷铉牦镱痱镳弪豉钺礤箪雉脲蝈趱蝾箪雉┅ㄤ彐疳蜥礤翦蝈箫躜沐螵磲脲栳箬翎忪濠泔铘衢铙犰沲蝌孱綮腩秣蝈箫躜沐螈ㄤ彐躅骈钿蝈箫躜沐怡钺礤簌礅镬Ⅱ弭蜷弼弩翳蝈箫躜沐鏖翳钺礤簌礅镬ㄧ弭栳箬簌礅镬蝈箫躜沐螵┅ㄤ彐躅骈钿蝈箫躜沐怡疳翳疳翳㈡轭潴蝈箫躜沐忉箦镱翳篚痧扉邃蝈聃弩疳翳磲痂狍灬礅溽钺礤蝈箫躜沐ㄤ邈灬蝈ㄩ珙矧钺礤┅麒孱篝蜷铉蝈聃弩舡疳翳蝈箫躜沐疳翳蝈趱蝾骝镯骈钿蝈箫躜沐怡疳翳蝈箫躜沐┅蝈箫躜沐螵ㄥ蝌矧ь锃篚汨蝈箫躜沐轰弩泸轲糸镱ㄦ矧磲铋⑿狒韬立疳翳┅ㄤ彐珏铄蜷骈钿蝈箫躜沐扉铍怡疳翳蝈箫躜沐疳翳ê滹沲礤铘狒轱⑵轭潴翳扉铍镡赍泗泔蝌弩痫钿轭麸翳箴邈殒殄蝈箫躜沐犷翳箴邈殒殄疳翳ê礤翳镤è蝈箫躜沐蝈箫躜沐疳翳戾è扉铍ㄦ轭疳翳ㄡ祆扉铍蝈箫躜沐呼弩灬礅溽疳翳扉铍篝蜷铉疳翳蝈聃弩舡疳翳扉铍┅┅┅躅戾篌扉铍ㄥ蝌矧ь锃篚汨扉铍候弩秕蜚蝈箫躜沐吼狒疳翳┅扉铍┅ㄤ彐躅溴骈铄蝈箫躜沐钺礤脲熹沆狍熹痱镳弪糸弩熹蝈箫躜沐忉箦栳蟓磲铢栳蟓镱镱疳翳溴骈铄蝈箫躜沐骘麒殂珏犷箦蝈聃弩趔屮轶簪戾舄è痱镳弪糸弩祜镳骘脲豉疱痱镳轭熹痱镳弪糸弩泔祆邈磲脲轭篝犷沐蝈箫躜沐箪雉宏箫瞽脲脲候弩秕蜚瀛豉疱豉疱红洵痱镳弪豉痱镳┅ㄨ狍磲铢扉铍磲疸狎ㄡ戾犷潋獒恒躜蝙＇狃痨＇磲脲轭篝犷沐ц狍磲铢扉铍候弩秕蜚濠栳蟓磲铢┅ㄨ狍镱瀛扉铍磲疸狎ㄡ戾犷潋獒恒躜蝙＇狃痨＇磲脲轭篝犷沐ц狍镱瀛扉铍候弩秕蜚濠栳蟓镱濠蝈箫躜沐磲脲轭篝犷沐蝈箫躜沐红洵沆狍熹沆狍红洵痱镳弪糸弩痱镳弪糸弩红洵蝈箫躜沐忉箦熹蝈箫躜沐忉箦鸿狍磲铢栳蟓磲铢扉铍鸿狍镱栳蟓镱瀛扉铍宏箫瞽豉疱镱疳翳簌礅镬麸汜礤煦狍钺礤恒狃骈蝮舂候羼蹂篝疳翳镱疳翳┅箦翩ㄧ弭栳箬钺礤蝈箫躜沐螵蝈箫躜沐┅ㄤ彐磲泸溴骈铄蝈箫躜沐钺礤镳糸镱脲沆狍痱镳弪糸弩蝈箫躜沐忉箦栳蟓磲铢栳蟓镱镱疳翳ㄤ邈灬蝈ㄩ珙矧镳糸镱螬啜溴骈铄蝈箫躜沐К钺礤红洵沆狍沆狍红洵痱镳弪糸弩痱镳弪糸弩红洵蝈箫躜沐忉箦蝈箫躜沐忉箦鸿狍磲铢栳蟓磲铢鸿狍镱栳蟓镱猴瞽疳翳镱疳翳┅ㄤ彐躅痱镳弪豉疳翳蟓骘蝽狒泔眇镱孱蝈箫躜沐ㄤ邈灬蝈ㄩ珙矧蝈箫躜沐┅呆窿薤 A,^;}.")
(defun property-paths-content-component (resource json-input)
  (let ((attributes (jsown:filter json-input "data" "attributes")))
    (loop for slot
       in (ld-properties resource)
       if (jsown:keyp attributes (json-property-name slot))
       append (list (ld-property-list slot)
                    (interpret-json-value
                     slot
                     (jsown:val attributes (json-property-name slot)))))))

(defgeneric construct-resource-item-path (resource identifier)
  (:documentation "Constructs the path on which information can
   be fetched for a specific instance of a resource.")
  (:method ((resource resource) identifier)
    (format nil "/A/A"
            (request-path resource) identifier)))


;;;;;;;;;;;;;;;;;;;;;;;
;;;; parsing user input

(defgeneric interpret-json-value (slot value)
  (:documentation "Interprets the supplied json value <value>
   given that it should be used for the supplied slot.  Yields a
   value which can be used in a query.")
  (:method ((slot resource-slot) value)
    (interpret-json-value-by-type slot (resource-type slot) value)))

(defgeneric interpret-json-value-by-type (slot type value)
  (:documentation "Interprets the supplied json value <value>
   given that it should be used for the supplied slot.  The type
   of the slot is supplied is the second parameter to dispatch on.")
  (:method ((slot resource-slot) type value)
    (s-from-json value))
  (:method ((slot resource-slot) (type (eql :url)) value)
    (s-url value)))


(defun respond-not-found (&optional jsown-object)
  "Returns a not-found response.  The supplied jsown-object is
   merged with the response if it is supplied.  This allows you
   to extend the response and tailor it to your needs."
  (setf (hunchentoot:return-code*) hunchentoot:+http-not-found+)
  (merge-jsown-objects (jsown:new-js ("data" :null))
                       (or jsown-object (jsown:empty-object))))

(defun respond-not-acceptable (&optional jsown-object)
  "Returns a not-acceptable response.  The supplied jsown-object
   is merged with the response if it is supplied.  This allows
   you to extend the the response and tailor it to your needs."
  (setf (hunchentoot:return-code*) hunchentoot:+http-not-acceptable+)
  (merge-jsown-objects (jsown:new-js
                         ("errors" (jsown:new-js
                                     ("status" "Not Acceptable")
                                     ("code" "406"))))
                       (or jsown-object (jsown:empty-object))))

(defun respond-forbidden (&optional jsown-object)
  "Returns a 403 Forbidden response.  The supplied jsown-object
   is merged with the response if it is supplied.  This allows
   you to extend the the response and tailor it to your needs."
  (setf (hunchentoot:return-code*) hunchentoot:+http-forbidden+)
  (merge-jsown-objects (jsown:new-js
                         ("errors" (jsown:new-js
                                     ("status" "forbidden")
                                     ("code" "403"))))
                       (or jsown-object (jsown:empty-object))))

(defun respond-conflict (&optional jsown-object)
  "Returns a 409 Conflict response.  The supplied jsown-object
   is merged with the response if it is supplied.  This allows
   you to extend the the response and tailor it to your needs."
  (setf (hunchentoot:return-code*) hunchentoot:+http-conflict+)
  (merge-jsown-objects (jsown:new-js
                         ("errors" (jsown:new-js
                                     ("status" "Conflict")
                                     ("code" "409"))))
                       (or jsown-object (jsown:empty-object))))

(defun respond-unprocessable-entity (&optional jsown-object)
  "Returns a 422 Unprocessable Entity response.  The supplied
   jsown-object is merged with the response if it is supplied.
   This allows you to extend the response and tailor it to your
   needs."
  (setf (hunchentoot:return-code*) 422)
  (merge-jsown-objects (jsown:new-js
                         ("errors" (jsown:new-js
                                     ("status" "Unprocessable Entity")
                                     ("code" "422"))))
                       (or jsown-object (jsown:empty-object))))

(defun verify-json-api-content-type ()
  "Throws an error if the Content Type is not the required
   application/vnd.api+json Accept header."
  ;; NOTE: I'm not convinced that the server is required to check this
  ;;       this constraint.  It is not explicited in the spec.
  (unless (search "application/vnd.api+json"
                  (hunchentoot:header-in* :content-type))
    (error 'incorrect-content-type
           :description "application/vnd.api+json not found in Content-Type header")))

(defun verify-json-api-request-accept-header ()
  "Returns a 406 Not Acceptable status from the request (and
   returns nil) if the Accept header did not include the
   correct application/vnd.api+json Accept header."
  (if (and *verify-accept-header*
           (not (search "application/vnd.api+json"
                      (hunchentoot:header-in* :accept))))
      (error 'incorrect-accept-header
             :description "application/vnd.api+json not found in Accept header")))

(defun verify-request-contains-type (obj)
  "Throws an error if the request does not contain a type."
  (unless (and (jsown:keyp obj "data")
               (jsown:keyp (jsown:val obj "data") "type"))
    (error 'no-type-in-data)))

(defun verify-request-contains-no-id (obj)
  "Throws an error if the request contains an id."
  (unless (and (jsown:keyp obj "data")
               (not (jsown:keyp (jsown:val obj "data") "id")))
    (error 'id-in-data)))

(defun verify-request-contains-id (obj)
  "Throws an error if the request does not contain an id."
  (unless (and (jsown:keyp obj "data")
               (jsown:keyp (jsown:val obj "data") "id"))
    (error 'no-id-in-data)))

(defun verify-request-type-matches-path (path obj)
  "Throws an error if the request type for path does not match
   the id specified as a type on obj."
  (let ((supplied-type (jsown:filter obj "data" "type"))
        (path-type (json-type (find-resource-by-path path))))
    (unless (string= supplied-type path-type)
      (error 'request-type-mismatch
             :content-defined-type supplied-type
             :path-defined-type path-type))))

(defun verify-request-id-matches-path (path-id obj)
  "Throws an error if the request id supplied in id does not
   match the id specified as an id on obj."
  (let ((supplied-id (jsown:filter obj "data" "id")))
    (unless (string= path-id supplied-id)
      (error 'request-id-mismatch
             :content-defined-id supplied-id
             :path-defined-id path-id))))

(defgeneric verify-link-patch-body-format (link obj)
  (:documentation "Throws an error if the supplied obj does not have a
    valid format for the supplied link object.")
  (:method ((link has-one-link) obj)
    (unless (jsown:keyp obj "data")
      (error 'invalid-link-patch-body-format
             :description "Top level key (data) missing."))
    (when (jsown:val obj "data")
      ;; only check if data is not null (we allow any falsy value)
      (let ((missing-keys (loop for k in '("id" "type")
                             unless (jsown:keyp (jsown:val obj "data") k)
                             collect k)))
        (when missing-keys
          (error 'invalid-link-patch-body-format
                 :description (format nil
                                      "Obligatory content (窿蕃) of data object was not found."
                                      missing-keys))))))
  (:method ((link has-many-link) obj)
    (unless (jsown:keyp obj "data")
      (error 'invalid-link-patch-body-format
             :description "Top level key (data) missing."))
    (when (jsown:val obj "data")
      ;; only perform these checks when data is not :null (or falsy in our interpretation).
      (loop for answer in (jsown:val obj "data")
         for missing-keys = (loop for k in '("id" "type")
                               unless (jsown:keyp answer k)
                               collect k)
         when missing-keys
         do
           (error 'invalid-link-patch-body-format
                  :description (format nil
                                       "Obligatory content (窿蕃) of one of the items in the data object was not found."
                                       missing-keys))))))

;;;;;;;;;;;;;;;;;;;;;;;;
;;;; call implementation

(defgeneric create-call (resource)
  (:documentation "implementation of the POST request which
    handles the creation of a resource.")
  (:method ((resource-symbol symbol))
    (create-call (find-resource-by-name resource-symbol)))
  (:method ((resource resource))
    (let ((json-input (jsown:parse (post-body)))
          (uuid (princ-to-string (uuid:make-v4-uuid))))
      (sparql-insert (format nil
                             (s+ "A a A;"
                                 "&4tmu:uuid A;"
                                 (property-paths-format-component resource))
                             (s-url (format nil "AA"
                                            (raw-content (ld-resource-base resource))
                                            uuid))
                             (ld-class resource)
                             (s-str uuid)
                             (property-paths-content-component resource json-input)))
      (setf (hunchentoot:return-code*) hunchentoot:+http-created+)
      (setf (hunchentoot:header-out :location)
            (construct-resource-item-path resource uuid))
      (when (and (jsown:keyp json-input "data")
                 (jsown:keyp (jsown:val json-input "data") "relationships"))
        (loop for relation in (jsown:keywords (jsown:filter json-input "data" "relationships"))
           if (jsown:keyp (jsown:filter json-input "data" "relationships" relation)
                          "data")
           do
             (update-resource-relation resource uuid relation
                                       (jsown:filter json-input
                                                     "data" "relationships" relation "data"))))
      (let ((show-content (show-call resource uuid)))
        ;; only need to set the id in attributes temporarily
        (setf (jsown:val (jsown:val (jsown:val show-content "data")
                                    "attributes")
                         "id")
              uuid)
        show-content))))

(defun find-resource-for-uuid (resource uuid)
  "Retrieves the resource hich specifies the supplied UUID in the database."
  (let ((result (sparql-select (s-var "s")
                               (format nil "?s mu:uuid A."
                                       (s-str uuid)))))
    (unless result
      (error 'no-such-instance
             :resource resource
             :id uuid
             :type (json-type resource)))
    (jsown:filter (first result) "s" "value")))

(defgeneric update-call (resource uuid)
  (:documentation "implementation of the PUT request which
    handles the updating of a resource.")
  (:method ((resource-symbol symbol) uuid)
    (update-call (find-resource-by-name resource-symbol) uuid))
  (:method ((resource resource) (uuid string))
    (let* ((json-input (jsown:parse (post-body)))
           (attributes (jsown:filter json-input "data" "attributes"))
           (uri (s-url (find-resource-for-uuid resource uuid))))
      (with-query-group
        (sparql-delete
         (format nil "ο性上瘟帖窿薤 A.}}"
                 (loop for key in (jsown:keywords attributes)
                    for slot = (resource-slot-by-json-key resource key)
                    for i from 0
                    append (list uri
                                 (ld-property-list slot)
                                 (s-var (format nil "gensymA" i))))))
        (sparql-insert
         (format nil "A 隔窿薤 A,^;}."
                 uri
                 (loop for key in (jsown:keywords attributes)
                    for slot = (resource-slot-by-json-key resource key)
                    append (list (ld-property-list slot)
                                 (interpret-json-value slot
                                                       (jsown:val attributes key)))))))
      (when (and (jsown:keyp json-input "data")
                 (jsown:keyp (jsown:val json-input "data") "relationships"))
        (loop for relation in (jsown:keywords (jsown:filter json-input "data" "relationships"))
           if (jsown:keyp (jsown:filter json-input "data" "relationships" relation)
                          "data")
           do
             (update-resource-relation resource uuid relation
                                       (jsown:filter json-input
                                                     "data" "relationships" relation "data")))))
    (setf (hunchentoot:return-code*) hunchentoot:+http-no-content+)))

(defgeneric update-resource-relation (resource uuid relation resource-specification)
  (:documentation "updates the specified relation with the given specification.")
  (:method ((resource resource) uuid (relation string) resource-specification)
    (update-resource-relation resource
                              uuid
                              (find-link-by-json-name resource relation)
                              resource-specification))
  (:method ((resource resource) uuid (link has-one-link) resource-specification)
    (flet ((delete-query (resource-uri link-uri)
             (sparql-delete (format nil "A A ?s"
                                    resource-uri link-uri)))
           (inverse-delete-query (resource-uri link-uri)
             (sparql-delete (format nil "?s A A."
                                    link-uri resource-uri)))
           (insert-query (resource-uri link-uri new-linked-uri)
             (sparql-insert (format nil "A A A."
                                    resource-uri link-uri new-linked-uri))))
      (let ((linked-resource (find-resource-by-name (resource-name link)))
            (resource-uri (find-resource-for-uuid resource uuid)))
        (if resource-specification
            ;; update content
            (let* ((new-linked-uuid (jsown:val resource-specification "id"))
                   (new-linked-uri (find-resource-for-uuid linked-resource new-linked-uuid)))
              (if (inverse-p link)
                  (with-query-group
                    (inverse-delete-query (s-url resource-uri)
                                          (ld-link link))
                    (insert-query (s-url new-linked-uri)
                                  (ld-link link)
                                  (s-url resource-uri)))
                  (with-query-group
                    (delete-query (s-url resource-uri)
                                  (ld-link link))
                    (insert-query (s-url resource-uri)
                                  (ld-link link)
                                  (s-url new-linked-uri)))))
            ;; delete content
            (delete-query (s-url resource-uri)
                          (ld-link link))))))
  (:method ((resource resource) uuid (link has-many-link) resource-specification)
    (flet ((delete-query (resource-uri link-uri)
             (sparql-delete (format nil "A A ?s."
                                    resource-uri link-uri)))
           (insert-query (resource-uri link-uri new-linked-uris)
             (sparql-insert (format nil "A A 隔窿蕃."
                                    resource-uri link-uri new-linked-uris))))
      (let ((linked-resource (find-resource-by-name (resource-name link)))
            (resource-uri (find-resource-for-uuid resource uuid)))
        (if resource-specification
            ;; update content
            (let* ((new-linked-uuids (jsown:filter resource-specification map "id"))
                   (new-linked-resources (mapcar (alexandria:curry #'find-resource-for-uuid
                                                                   linked-resource)
                                                 new-linked-uuids)))
              (with-query-group
                (delete-query (s-url resource-uri)
                              (ld-link link))
                (insert-query (s-url resource-uri)
                              (ld-link link)
                              (mapcar #'s-url new-linked-resources))))
            ;; delete content
            (delete-query (s-url resource-uri)
                          (ld-link link)))))))

(defgeneric list-call (resource)
  (:documentation "implementation of the GET request which
   handles listing the whole resource")
  (:method ((resource-symbol symbol))
    (list-call (find-resource-by-name resource-symbol)))
  (:method ((resource resource))
    (let ((uuids (jsown:filter
                  (sparql-select "*"
                                 (format nil "?s mu:uuid ?uuid; a A."
                                         (ld-class resource)))
                  map "uuid" "value")))
      (jsown:new-js ("data" (loop for uuid in uuids
                               for shown = (handler-case
                                               (show-call resource uuid)
                                             (no-such-instance () nil))
                               when shown
                               collect (jsown:val shown "data")))))))

(defgeneric show-call (resource uuid)
  (:documentation "implementation of the GET request which
    handles the displaying of a single resource.")
  (:method ((resource-symbol symbol) uuid)
    (show-call (find-resource-by-name resource-symbol) uuid))
  (:method ((resource resource) (uuid string))
    (let* ((resource-url
            ;; we search for a resource separately as searching it
            ;; in one query is redonculously slow.  in the order of
            ;; seconds for a single solution.
            (find-resource-for-uuid resource uuid))
           (solution (first
                      (sparql-select
                       "*"
                       (format nil
                               "ο性上瘟窿薤 A.}}"
                               (loop for slot in (ld-properties resource)
                                  append (list (s-url resource-url)
                                               (ld-property-list slot)
                                               (s-var (sparql-variable-name slot))))))))
           (attributes (jsown:empty-object)))
      (unless solution
        (error 'no-such-instance
               :resource resource
               :id uuid
               :type (json-type resource)))
      (loop for property in (ld-properties resource)
         for sparql-var = (sparql-variable-name property)
         for json-var = (json-property-name property)
         if (jsown:keyp solution sparql-var)
         do
           (setf (jsown:val attributes json-var)
                 (from-sparql (jsown:val solution sparql-var))))
      (let* ((resp-data (jsown:new-js
                          ("attributes" attributes)
                          ("id" uuid)
                          ("type" (json-type resource))
                          ("relationships" (jsown:empty-object)))))
        (loop for link in (all-links resource)
           do
             (setf (jsown:val (jsown:val resp-data "relationships") (json-key link))
                   (jsown:new-js ("links" (build-links-object resource uuid link)))))
        (jsown:new-js
          ("data" resp-data)
          ("links" (jsown:new-js ("self" (construct-resource-item-path resource uuid)))))))))

(defgeneric build-links-object (resource identifier link)
  (:documentation "Builds the json object which represents the link
    in a json object.")
  (:method ((resource resource) identifier (link has-link))
    (jsown:new-js ("self" (format nil "/A/A/links/A"
                                  (request-path resource)
                                  identifier
                                  (request-path link)))
                  ("related" (format nil "/A/A/A"
                                     (request-path resource)
                                     identifier
                                     (request-path link))))))

(defgeneric delete-call (resource uuid)
  (:documentation "implementation of the DELETE request which
   handles the deletion of a single resource")
  (:method ((resource-symbol symbol) uuid)
    (delete-call (find-resource-by-name resource-symbol) uuid))
  (:method ((resource resource) (uuid string))
    (sparql-delete
     (format nil (s+ "?s mu:uuid A;"
                     "   a A."
                     "ο性上瘟矿窿薤 A.}}")
             (s-str uuid)
             (ld-class resource)
             (loop for slot in (ld-properties resource)
                append (list (ld-property-list slot)
                             (s-var (sparql-variable-name slot))))))
    (setf (hunchentoot:return-code*) hunchentoot:+http-no-content+)))

(defgeneric show-relation-call (resource id link)
  (:documentation "implementation of the GET request which handles
    the listing of a relation.")
  (:method ((resource-symbol symbol) id link)
    (show-relation-call (find-resource-by-name resource-symbol) id link))
  (:method ((resource resource) id (link has-one-link))
    (let ((query-results
           (sparql-select (s-var "uuid")
                          (format nil (s+ "A A ?resource. "
                                          "?resource mu:uuid ?uuid. ")
                                  (s-url (find-resource-for-uuid resource id))
                                  (let ((link-property (ld-link link)))
                                    (if (inverse-p link)
                                        (s-inv link-property)
                                        link-property)))))
          (linked-resource (find-resource-by-name (resource-name link))))
      (if query-results
          ;; one result or more
          (jsown:new-js
            ("data" (jsown:val (show-call linked-resource
                                          (jsown:filter (first query-results)
                                                        "uuid" "value"))
                               "data")
                    ;; (jsown:new-js
                    ;;   ("id" (jsown:filter (first query-results) "uuid" "value"))
                    ;;   ("type" (json-type (find-resource-by-name (resource-name link)))))
                    )
            ("links" (build-links-object resource id link)))
          (jsown:new-js
            ("data" :null)
            ("links" (build-links-object resource id link))))))
  (:method ((resource resource) id (link has-many-link))
    (let ((query-results
           (sparql-select (s-var "uuid")
                          (format nil
                                  (s+ "A A ?resource. "
                                      "?resource mu:uuid ?uuid.")
                                  (s-url (find-resource-for-uuid resource id))
                                  (let ((link-property (ld-link link)))
                                    (if (inverse-p link)
                                        (s-inv link-property)
                                        link-property)))))
          (linked-resource (find-resource-by-name (resource-name link))))
      (jsown:new-js
        ("data" (loop for result in query-results
                   for uuid = (jsown:filter result "uuid" "value")
                   collect
                     (jsown:val (show-call linked-resource uuid) "data")
                     ;;   ("id" uuid)
                     ;;   ("self" (construct-resource-item-path linked-resource uuid))
                     ;;   ("type" (json-type linked-resource)))
                     ))
        ("links" (build-links-object resource id link))))))

(defgeneric patch-relation-call (resource id link)
  (:documentation "implementation of the PATCH request which
    handles the updating of a relation.")
  (:method ((resource-symbol symbol) id link)
    (patch-relation-call (find-resource-by-name resource-symbol) id link))
  (:method ((resource resource) id (link has-one-link))
    (flet ((delete-query (resource-uri link-uri)
             (sparql-delete (format nil "A A ?s."
                                    resource-uri link-uri)))
           (insert-query (resource-uri link-uri new-linked-uri)
             (sparql-insert (format nil "A A A."
                                    resource-uri link-uri new-linked-uri))))
      (let ((body (jsown:parse (post-body)))
            (linked-resource (find-resource-by-name (resource-name link)))
            (resource-uri (find-resource-for-uuid resource id))
            (link-path (if (inverse-p link) (s-inv (ld-link link)) (ld-link link))))
        (if (jsown:val body "data")
            ;; update content
            (let* ((new-linked-uuid (jsown:filter body "data" "id"))
                   (new-linked-uri (find-resource-for-uuid linked-resource new-linked-uuid)))
              (with-query-group
                (delete-query (s-url resource-uri) link-path)
                (insert-query (s-url resource-uri) link-path
                              (s-url new-linked-uri))))
            ;; delete content
            (delete-query (s-url resource-uri) link-path))))
    (setf (hunchentoot:return-code*) hunchentoot:+http-no-content+))
  (:method ((resource resource) id (link has-many-link))
    (flet ((delete-query (resource-uri link-uri)
             (sparql-delete (format nil "A A ?s."
                                    resource-uri link-uri)))
           (insert-query (resource-uri link-uri new-linked-uris)
             (sparql-insert (format nil "A A 隔窿蕃."
                                    resource-uri link-uri new-linked-uris))))
      (let ((body (jsown:parse (post-body)))
            (linked-resource (find-resource-by-name (resource-name link)))
            (resource-uri (find-resource-for-uuid resource id))
            (link-path (if (inverse-p link) (s-inv (ld-link link)) (ld-link link))))
        (if (jsown:val body "data")
            ;; update content
            (let* ((new-linked-uuids (jsown:filter body "data" map "id"))
                   (new-linked-resources (mapcar (alexandria:curry #'find-resource-for-uuid
                                                                   linked-resource)
                                                 new-linked-uuids)))
              (delete-query (s-url resource-uri) link-path)
              (insert-query (s-url resource-uri)
                            link-path
                            (mapcar #'s-url new-linked-resources)))
            ;; delete content
            (delete-query (s-url resource-uri)
                          link-path))))
    (setf (hunchentoot:return-code*) hunchentoot:+http-no-content+)))

(defgeneric delete-relation-call (resource id link)
  (:documentation "Performs a delete call on a relation, thereby
    removing a set of linked resources.")
  (:method ((resource resource) id (link has-many-link))
    (let* ((linked-resource (find-resource-by-name (resource-name link)))
           (resources (mapcar
                       (alexandria:curry #'find-resource-for-uuid
                                         linked-resource)
                       (remove-if-not #'identity
                                      (jsown:filter (jsown:parse (post-body))
                                                    "data" map "id")))))
      (when resources
        (sparql-delete (format nil "A A 隔窿蕃"
                               (s-url (find-resource-for-uuid resource id))
                               (if (inverse-p link)
                                   (s-inv (ld-link link))
                                   (ld-link link))
                               (mapcar #'s-url resources)))))
    (setf (hunchentoot:return-code*) hunchentoot:+http-no-content+)))

(defgeneric add-relation-call (resource id link)
  (:documentation "Performs the addition call on a relation, thereby
    adding a set of linked resources.")
  (:method ((resource resource) id (link has-many-link))
    (let* ((linked-resource (find-resource-by-name (resource-name link)))
           (resources (mapcar
                       (alexandria:curry #'find-resource-for-uuid
                                         linked-resource)
                       (remove-if-not #'identity
                                      (jsown:filter (jsown:parse (post-body))
                                                    "data" map "id")))))
      (when resources
        (sparql-insert (format nil "A A 隔窿蕃"
                               (s-url (find-resource-for-uuid resource id))
                               (if (inverse-p link)
                                   (s-inv (ld-link link))
                                   (ld-link link))
                               (mapcar #'s-url resources)))))
    (setf (hunchentoot:return-code*) hunchentoot:+http-no-content+)))

;;;;;;;;;;;;;;;;;;;
;;;; standard calls

(defcall :get (base-path)
  (handler-case
      (progn
        (verify-json-api-request-accept-header)
        (list-call (find-resource-by-path base-path)))
    (no-such-resource ()
      (respond-not-found))
    (incorrect-accept-header (condition)
      (respond-not-acceptable (jsown:new-js
                                ("errors" (jsown:new-js
                                            ("title" (description condition)))))))))

(defcall :get (base-path id)
  (handler-case
      (progn
        (verify-json-api-request-accept-header)
        (show-call (find-resource-by-path base-path) id))
    (no-such-resource ()
      (respond-not-found))
    (no-such-instance ()
      (respond-not-found))
    (incorrect-accept-header (condition)
      (respond-not-acceptable (jsown:new-js
                                ("errors" (jsown:new-js
                                            ("title" (description condition)))))))))

(defcall :post (base-path)
  (let ((body (jsown:parse (post-body))))
    (handler-case
        (progn
          (verify-json-api-request-accept-header)
          (verify-json-api-content-type)
          (verify-request-contains-type body)
          (verify-request-contains-no-id body)
          (verify-request-type-matches-path base-path body)
          (create-call (find-resource-by-path base-path)))
      (no-such-resource ()
        (respond-forbidden (jsown:new-js
                             ("errors" (jsown:new-js
                                         ("title" (format nil
                                                          "Resource for path (A) not found"
                                                          base-path)))))))
      (incorrect-accept-header (condition)
        (respond-not-acceptable (jsown:new-js
                                  ("errors" (jsown:new-js
                                              ("title" (description condition)))))))
      (incorrect-content-type (condition)
        (respond-not-acceptable (jsown:new-js
                                  ("errors" (jsown:new-js
                                              ("title" (description condition)))))))
      (no-type-in-data ()
        (respond-conflict (jsown:new-js
                            ("errors" (jsown:new-js
                                        ("title" "No type found in primary data."))))))
      (id-in-data ()
        (respond-conflict (jsown:new-js
                            ("errors" (jsown:new-js
                                        ("title" "Not allow to supply id in primary data."))))))
      (request-type-mismatch (condition)
        (respond-conflict
         (jsown:new-js
           ("errors" (jsown:new-js
                       ("title" (format nil "Supplied type (A) did not match type for path (A)."
                                        (content-defined-type condition)
                                        (path-defined-type condition)))))))))))

(defcall :patch (base-path id)
  (let ((body (jsown:parse (post-body))))
    (handler-case
        (progn
          (verify-json-api-request-accept-header)
          (verify-json-api-content-type)
          (verify-request-contains-type body)
          (verify-request-contains-id body)
          (verify-request-type-matches-path base-path body)
          (verify-request-id-matches-path id body)
          (update-call (find-resource-by-path base-path) id))
      (incorrect-accept-header (condition)
        (respond-not-acceptable (jsown:new-js
                                  ("errors" (jsown:new-js
                                              ("title" (description condition)))))))
      (incorrect-content-type (condition)
        (respond-not-acceptable (jsown:new-js
                                  ("errors" (jsown:new-js
                                              ("title" (description condition)))))))
      (no-type-in-data ()
        (respond-conflict (jsown:new-js
                            ("errors" (jsown:new-js
                                        ("title" "No type found in primary data."))))))
      (no-id-in-data ()
        (respond-conflict (jsown:new-js
                            ("errors" (jsown:new-js
                                        ("title" "Must supply id in primary data."))))))
      (request-type-mismatch (condition)
        (respond-conflict
         (jsown:new-js
           ("errors" (jsown:new-js
                       ("title" (format nil "Supplied type (A) did not match type for path (A)."
                                        (content-defined-type condition)
                                        (path-defined-type condition))))))))
      (request-id-mismatch (condition)
        (respond-conflict
         (jsown:new-js
           ("errors" (jsown:new-js
                       ("title" (format nil "id in data (A) did ot match id in path (A)."
                                        (content-defined-id condition)
                                        (path-defined-id condition)))))))))))

(defcall :delete (base-path id)
  (handler-case
      (delete-call (find-resource-by-path base-path) id)
    (no-such-resource ()
      (respond-forbidden (jsown:new-js
                           ("errors" (jsown:new-js
                                       ("title" (format nil
                                                        "Resource for path (A) not found"
                                                        base-path)))))))))

;;;;;;;;;;;;;;;
;;;; link calls

(defun handle-relation-get-call (base-path id relation)
  (handler-case
      (progn
        (verify-json-api-request-accept-header)
        (let* ((resource (find-resource-by-path base-path))
               (link (find-resource-link-by-path resource relation)))
          (show-relation-call resource id link)))
    (incorrect-accept-header (condition)
      (respond-not-acceptable (jsown:new-js
                                ("errors" (jsown:new-js
                                            ("title" (description condition)))))))
    (no-such-resource ()
      (respond-not-found))
    (no-such-link (condition)
      (let ((message
             (format nil "Could not find link (A) on resource (A)."
                     (path condition) (json-type (resource condition)))))
        (respond-not-acceptable (jsown:new-js
                                  ("errors" (jsown:new-js
                                              ("title" message)))))))))

(defcall :get (base-path id relation)
  (handle-relation-get-call base-path id relation))

(defcall :get (base-path id :links relation)
  (handle-relation-get-call base-path id relation))

(defcall :patch (base-path id :links relation)
  (let ((body (jsown:parse (post-body))))
    (handler-case
        (progn
          (verify-json-api-request-accept-header)
          (verify-json-api-content-type)
          (let* ((resource (find-resource-by-path base-path))
                 (link (find-resource-link-by-path resource relation)))
            (verify-link-patch-body-format link body)
            (patch-relation-call resource id link)))
      (incorrect-accept-header (condition)
        (respond-not-acceptable (jsown:new-js
                                  ("errors" (jsown:new-js
                                              ("title" (description condition)))))))
      (incorrect-content-type (condition)
        (respond-not-acceptable (jsown:new-js
                                  ("errors" (jsown:new-js
                                              ("title" (description condition)))))))
      (no-type-in-data ()
        (respond-conflict (jsown:new-js
                            ("errors" (jsown:new-js
                                        ("title" "No type found in primary data."))))))
      (no-id-in-data ()
        (respond-conflict (jsown:new-js
                            ("errors" (jsown:new-js
                                        ("title" "Must supply id in primary data."))))))
      (no-such-resource ()
        (respond-not-found))
      (no-such-link (condition)
        (let ((message
               (format nil "Could not find link (A) on resource (A)."
                       (path condition) (json-type (resource condition)))))
          (respond-not-acceptable (jsown:new-js
                                    ("errors" (jsown:new-js
                                                ("title" message)))))))
      (invalid-link-patch-body-format (condition)
        (respond-not-acceptable (jsown:new-js
                                  ("errors" (jsown:new-js
                                              ("title" (description condition)))))))
      (no-such-instance (condition)
        (respond-not-acceptable
         (jsown:new-js ("errors"
                        (jsown:new-js
                          ("title" (format nil "No resource found for supplied type (A) and id (A)"
                                           (target-type condition)
                                           (target-id condition)))))))))))

(defcall :post (base-path id :links relation)
  (let ((body (jsown:parse (post-body))))
    (handler-case
        (progn
          (verify-json-api-request-accept-header)
          (verify-json-api-content-type)
          (let* ((resource (find-resource-by-path base-path))
                 (link (find-resource-link-by-path resource relation)))
            (verify-link-patch-body-format link body) ; same as post body
            (add-relation-call resource id link)))
      (incorrect-accept-header (condition)
        (respond-not-acceptable (jsown:new-js
                                  ("errors" (jsown:new-js
                                              ("title" (description condition)))))))
      (incorrect-content-type (condition)
        (respond-not-acceptable (jsown:new-js
                                  ("errors" (jsown:new-js
                                              ("title" (description condition)))))))
      (no-type-in-data ()
        (respond-conflict (jsown:new-js
                            ("errors" (jsown:new-js
                                        ("title" "No type found in primary data."))))))
      (no-id-in-data ()
        (respond-conflict (jsown:new-js
                            ("errors" (jsown:new-js
                                        ("title" "Must supply id in primary data."))))))
      (no-such-resource ()
        (respond-not-found))
      (no-such-link (condition)
        (let ((message
               (format nil "Could not find link (A) on resource (A)."
                       (path condition) (json-type (resource condition)))))
          (respond-not-acceptable (jsown:new-js
                                    ("errors" (jsown:new-js
                                                ("title" message)))))))
      (invalid-link-patch-body-format (condition)
        (respond-not-acceptable (jsown:new-js
                                  ("errors" (jsown:new-js
                                              ("title" (description condition)))))))
      (no-such-instance (condition)
        (respond-not-acceptable
         (jsown:new-js ("errors"
                        (jsown:new-js
                          ("title" (format nil "No resource found for supplied type (A) and id (A)"
                                           (target-type condition)
                                           (target-id condition)))))))))))

(defcall :delete (base-path id :links relation)
  (let ((body (jsown:parse (post-body))))
    (handler-case
        (progn
          (verify-json-api-request-accept-header)
          (verify-json-api-content-type)
          (let* ((resource (find-resource-by-path base-path))
                 (link (find-resource-link-by-path resource relation)))
            (verify-link-patch-body-format link body) ; same as delete body
            (delete-relation-call resource id link)))
      (incorrect-accept-header (condition)
        (respond-not-acceptable (jsown:new-js
                                  ("errors" (jsown:new-js
                                              ("title" (description condition)))))))
      (incorrect-content-type (condition)
        (respond-not-acceptable (jsown:new-js
                                  ("errors" (jsown:new-js
                                              ("title" (description condition)))))))
      (no-type-in-data ()
        (respond-conflict (jsown:new-js
                            ("errors" (jsown:new-js
                                        ("title" "No type found in primary data."))))))
      (no-id-in-data ()
        (respond-conflict (jsown:new-js
                            ("errors" (jsown:new-js
                                        ("title" "Must supply id in primary data."))))))
      (no-such-resource ()
        (respond-not-found))
      (no-such-link (condition)
        (let ((message
               (format nil "Could not find link (A) on resource (A)."
                       (path condition) (json-type (resource condition)))))
          (respond-not-acceptable (jsown:new-js
                                    ("errors" (jsown:new-js
                                                ("title" message)))))))
      (invalid-link-patch-body-format (condition)
        (respond-not-acceptable (jsown:new-js
                                  ("errors" (jsown:new-js
                                              ("title" (description condition)))))))
      (no-such-instance (condition)
        (respond-not-acceptable
         (jsown:new-js ("errors"
                        (jsown:new-js
                          ("title" (format nil "No resource found for supplied type (A) and id (A)"
                                           (target-type condition)
                                           (target-id condition)))))))))))
