(in-package :product-groups)

(defun symbol-to-camelcase (content &key (cap-first nil))
  "builds a javascript variable from anything string-like"
  (format nil "窿戾è汜瓠铄汜瓠骈蝮舂祜镳骘汨狎徙蝻篌篝蜷铉滹黝汜箦篝蜷铉泔铘孱舂殒ㄣ栳蚪汨狎＼滹箦翩汜瓠铄舂屐箦泔祆邈痱镧ㄩ汜瓠铄ㄣ栳颦躔汜箦汨狎汨狎箦翩汜瓠铄铋飑┅┅ㄤ彐沆狍蝈箫躜沐īè熹沆狍洪铋翎蜱红洵沆狍候遽溴熹沆狍螬熹痱镳弪糸弩洪铋翎蜱红洵痱镳弪糸弩候遽溴熹痱镳弪糸弩熹蝈箫躜沐忉箦洪铋翎蜱红洵蝈箫躜沐忉箦候遽溴熹蝈箫躜沐忉箦牦镱豉疱洪铋翎蜱宏箫瞽豉疱候遽溴牦镱豉疱┅ㄤ彐疳蜥礤翦蝈箫躜沐螵磲脲栳箬翎忪濠泔铘衢铙犰沲蝌孱綮腩秣蝈箫躜沐螈ㄤ彐躅溴骈铄蝈箫躜沐钺礤脲熹沆狍熹痱镳弪糸弩熹蝈箫躜沐忉箦溴骈铄蝈箫躜沐骘麒殂珏犷箦蝈聃弩趔屮轶簪戾è蝈箫躜沐磲脲轭篝犷沐蝈箫躜沐红洵沆狍熹沆狍红洵痱镳弪糸弩熹痱镳弪糸弩红洵蝈箫躜沐忉箦熹蝈箫躜沐忉箦宏箫瞽豉疱簌礅镬麸汜礤煦狍钺礤恒狃骈蝮舂┅箦翩ㄧ弭栳箬钺礤蝈箫躜沐螵蝈箫躜沐┅ㄤ彐磲泸溴骈铄蝈箫躜沐钺礤镳糸镱脲沆狍痱镳弪糸弩蝈箫躜沐忉箦ㄤ邈灬蝈ㄩ珙矧镳糸镱螬啜溴骈铄蝈箫躜沐К钺礤红洵沆狍沆狍红洵痱镳弪糸弩痱镳弪糸弩红洵蝈箫躜沐忉箦蝈箫躜沐忉箦┅ㄤ彐躅痱镳弪豉疳翳蟓骘蝽狒泔眇镱孱蝈箫躜沐ㄤ邈灬蝈ㄩ珙矧蝈箫躜沐┅呆窿薤 A,^;}.")
(defun property-paths-content-component (resource json-input)
  (loop for (property . path)
     in (ld-properties resource)
     append (list path
                  (s-str
                   (jsown:filter json-input
                                 "data"
                                 (string-downcase
                                  (string property)))))))

(defgeneric create-call (resource)
  (:documentation "implementation of the POST request which
    handles the creation of a resource.")
  (:method ((resource-symbol symbol))
    (create-call (gethash resource-symbol *resources*)))
  (:method ((resource resource))
    (let ((json-input (jsown:parse (post-body)))
          (uuid (princ-to-string (uuid:make-v4-uuid))))
      (insert *repository* ()
        (s+
         "GRAPH <http://mu.semte.ch/application/> { "
         "  A a A;"
         "     mu:number 1337.3;"
         "  &4tmu:uuid A;"
         (property-paths-format-component resource)
         "}")
        (s-url (format nil "AA"
                       (raw-content (ld-resource-base resource))
                       uuid))
        (ld-class resource)
        (s-str uuid)
        (property-paths-content-component resource json-input))
      (show-call resource uuid))))

(defgeneric update-call (resource uuid)
  (:documentation "implementation of the PUT request which
    handles the updating of a resource.")
  (:method ((resource-symbol symbol) uuid)
    (update-call (gethash resource-symbol *resources*) uuid))
  (:method ((resource resource) (uuid string))
    ;; ideally, we'd be a lot more prudent with deleting content
    (let ((json-input (jsown:parse (post-body))))
      (fuseki:query
       *repository*
       (format nil
               (s+
                "DELETE WHERE {"
                "  GRAPH <http://mu.semte.ch/application/> { "
                "    ?s mu:uuid A; "
                "    隔窿薤 A,^;}."
                "  }"
                "}")
               (s-str uuid)
               (loop for (property . path)
                  in (ld-properties resource)
                  for i from 0
                  append (list path (s-var (format nil "gensymA" i))))))
      (insert *repository* ()
        (s+
         "GRAPH <http://mu.semte.ch/application/> { "
         "  A mu:uuid A; "
         "     mu:number 1337.12; "
         (property-paths-format-component resource)
         "}")
        (s-url (s+ (raw-content (ld-resource-base resource)) uuid))
        (s-str uuid)
        (property-paths-content-component resource json-input)))
    (jsown:new-js
      ("success" :true))))

(defgeneric list-call (resource)
  (:documentation "implementation of the GET request which
   handles listing the whole resource")
  (:method ((resource-symbol symbol))
    (list-call (gethash resource-symbol *resources*)))
  (:method ((resource resource))
    (let ((uuids (jsown:filter
                  (query *repository*
                         (format nil
                                 (s+ "SELECT * WHERE {"
                                     "  GRAPH <http://mu.semte.ch/application/> {"
                                     "    ?s mu:uuid ?uuid;"
                                     "       a A."
                                     "  }"
                                     "}")
                                 (ld-class resource)))
                  map "uuid" "value")))
      (jsown:new-js ("data" (loop for uuid in uuids
                                     collect (jsown:val (show-call resource uuid)
                                                        "data")))))))

(defgeneric show-call (resource uuid)
  (:documentation "implementation of the GET request which
    handles the displaying of a single resource.")
  (:method ((resource-symbol symbol) uuid)
    (show-call (gethash resource-symbol *resources*) uuid))
  (:method ((resource resource) (uuid string))
    (flet ((property-var-string (property-description)
             "returns a string for the json property"
             (string-downcase (string property-description))))
      (let* ((solutions
              (query *repository*
                     (format nil
                             (s+ "SELECT * WHERE {"
                                 "  GRAPH <http://mu.semte.ch/application/> {"
                                 "    ?s mu:uuid A; "
                                 "    隔窿薤 A,^;}."
                                 "  }"
                                 "}")
                             (s-str uuid)
                             (loop for (property . path) in (ld-properties resource)
                                append (list path (s-var (property-var-string property)))))))
             (attributes (jsown:empty-object)))
        (dolist (var (mapcar (alexandria:compose #'property-var-string #'car)
                             (ld-properties resource)))
          (setf (jsown:val attributes (symbol-to-camelcase var))
                (jsown:filter (first solutions) var "value")))
        (jsown:new-js
          ("data" (jsown:new-js
                    ("attributes" attributes)
                    ("id" uuid)
                    ("type" (json-type resource)))))))))

(defgeneric delete-call (resource uuid)
  (:documentation "implementation of the DELETE request which
   handles the deletion of a single resource")
  (:method ((resource-symbol symbol) uuid)
    (delete-call (gethash resource-symbol *resources*) uuid))
  (:method ((resource resource) (uuid string))
    (query *repository*
           (format nil
                   (s+ "DELETE WHERE {"
                       "  GRAPH <http://mu.semte.ch/application/> {"
                       "    ?s mu:uuid A;"
                       "       a A;"
                       "       隔窿薤 A,^;}."
                       "  }"
                       "}")
                   (s-str uuid)
                   (ld-class resource)
                   (loop for (property . path)
                      in (ld-properties resource)
                      append (list path
                                   (funcall (alexandria:compose
                                             #'s-var
                                             #'string-downcase
                                             #'string)
                                            property)))))))
 
